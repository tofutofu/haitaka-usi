// USI - Universal Shogi Interface protocol
// ----------------------------------------
//
// References: 
// - UCI protocol (https://backscattering.de/chess/uci/)
// - The Universal Shogi Interface (http://hgm.nubati.net/usi.html)
// - https://shogidokoro2.stars.ne.jp/usi.html 
//
// Both the UCI protocol and the draft version of the USI protocol  (second link above)
// are vague about details, edge cases and error conditions. The UCI protocol also made
// some unfortunate design decisions that make the protocol brittle and more difficult 
// to implement. In particular, the decision to ***silently  ignore*** junk tokens was 
// questionable. In the grammar implemented here, I take the Japanese Shogidokoro spec 
// as basis since it's overall clearer about details and about actual usage.
//
// Requirements
// ------------
// - All communication between engine and GUI is done by 7-bit ASCII text.
//
//   This seems to imply that non-ascii text _should_ be rejected. The spec does not specify 
//   if this also applies to the `info string ...` and `info filename ...` commands. There is
//   also no length restriction on those two commands; in some cases this could pose security 
//   risks. In the grammar below I strictly require ASCII in all commands including option 
//   names and values, but I do allow non-ASCII characters in junk portions.
//
// - All command strings must end with newline. All viable end-of-line characters should be
//   handled ('\n', '\r', or any combination). PEST NEWLINE handles this.
//
// - Arbitrary white space between tokens is allowed. The spec does not define "white space".
//   I define it as ASCII space or tab (' ', '\t', excluding '\v' and '\f').
//
// - If the engine or the GUI receives an unknown command or token "it should just ignore it 
//   and try to parse the rest of the line". The draft spec gives as examples:
//   - `joho debug on\n` should turn the debug mode on given that joho is not defined, 
//   - `debug joho on\n` will be undefined however.
//   Apparently this means that unparsable _prefixes_ should be ignored. In the grammar here
//   I allow leading junk but do not allow trailing junk on the same line. Allowing trailing
//   junk could make the semantics ambiguous, and could make it impossible to distinguish
//   between errors that can safely be ignored and those that should not be.
//
// - If the engine receives a command that is unexpected, for example `stop` when 
//   the engine is not calculating, it should also just ignore it. This is not handled by
//   the syntax, but can only be handled by the Engine code.
//
// - All options with fixed semantics start with the prefix "USI_". For instance, the option 
//   which is named "Hash" in UCI is named "USI_Hash" in USI. This is not handled by the grammar. 
//   The only restriction here is that option names should all be in ASCII and  should not contain 
//   whitespace (the UCI protocol does allow white-space in option names).
// 
// Case-sensitivity
// ----------------
// The spec doesn't specify whether or not command tokens are case-sensitive. Stockfish only accepts 
// lowercased commands. In the grammar below all command tokens and key words are expected to be 
// ASCII lower case. 
//
// Errors and tokens
// -----------------
// The published specs say nothing about error conditions or about what to do in case of error. 
// For instance: Should `go ponderosa` be parsed as `go ponder`, ignoring the trailing `osa`? Or 
// should it be an error? Should `debugon` be parsed as `debug on` or as junk? How about "usi usi"?
// Since the spec talks about "tokens" (without defining those!), it seems reasonable to interpret 
// all keywords as white-space-separated "tokens" and to require that all tokens must be delimited 
// either by white-space (excluding newlines), SOI (start of input) or EOI (end of input). 
//
// PEST/PEG
// --------
//
// `ANY` matches any single Unicode code point, including whitespace and newline. This is not
// clearly documented, but can be verified in the fiddle editor (https://pest.rs/#editor). The
// grammar below only using ANY in junk messages. All regular messages need to consist of ASCII.
//

WHITESPACE = _{ " " | "\t" }
WS = _{ WHITESPACE+ }
NL = _{ WHITESPACE | NEWLINE }
SEP = _{"/"}

// start 
start = _{SOI ~ line+ ~ &EOI}
line = _{ other? ~ (delimited_message | junk) }

delimited_message = _{ NL* ~ message ~ WHITESPACE* ~ NEWLINE }

message = _{ gui_message | engine_message | other}

// other 
other = @{ (!gui_message ~ !engine_message ~ !NEWLINE ~ ANY)+ }
junk = @{ (!NEWLINE ~ ANY)* ~ NEWLINE}

// gui message
gui_message = _{
    usi | 
    debug | 
    isready | 
    setoption | 
    register_user | 
    usinewgame | 
    stop | 
    ponderhit | 
    quit |
    position | 
    go |
    gameover
}

// engine message
engine_message = _{
    id | 
    usiok | 
    readyok | 
    bestmove | 
    copyprotection | 
    registration | 
    option | 
    info
}

//
// GuiMessage: GUI to engine
//

usi = ${ "usi" ~ &NL }

debug = ${ "debug" ~ (WS ~ ("on" | "off"))?  }

isready = ${ "isready"  }

setoption = ${ "setoption" ~ WS ~ "name" ~ WS ~ setoption_name ~ (WS ~ "value" ~ WS ~ setoption_value)? }

    setoption_name = ${ !("value") ~ token }
    setoption_value = ${ token }

register_user = ${ register_later | register_with_name_and_code }

    register_later = { "register" ~ WS ~ "later" }
    register_with_name_and_code = { "register" ~ WS ~ "name" ~ WS ~ register_name ~ WS ~ "code" ~ WS ~ register_code }
    register_name = { register_token ~ (WS ~ register_token)* }
    register_token = _{ !("code") ~ token }
    register_code = { tokens }

usinewgame = ${ "usinewgame" }

stop = ${ "stop"  }

quit = ${ "quit"  }

ponderhit = ${ "ponderhit"  }

gameover = ${ "gameover" ~ WS ~ (win | lose | draw)  }
    
    win = { "win" }
    lose = { "lose" }
    draw = { "draw" }

position = ${ "position" ~ WS ~ (startpos | sfenpos) ~ (WS ~ "moves" ~ WS ~ moves)?  }

    startpos = { "startpos" }
    sfenpos = { "sfen" ~ WS ~ sfen_board ~ WS ~ sfen_color ~ WS ~ sfen_hands ~ (WS ~ sfen_move_num)? }

    sfen_board = { (sfen_rank ~ SEP){8} ~ sfen_rank }
    sfen_color = { "w" | "b" }
    sfen_hands = { sfen_black_hand ~ sfen_white_hand? | sfen_white_hand | sfen_empty_hand }
    sfen_move_num = { digits }  

    sfen_rank = { ((prom? ~ (white_piece | black_piece)) | file){1,9} }

    sfen_black_hand = { (npieces? ~ black_hand_piece){1,6} }
    sfen_white_hand = { (npieces? ~ white_hand_piece){1,6} }
    sfen_empty_hand = { "-" }

    prom = { "+" }
    white_piece = { "k" | "r"  | "b" | "g" | "s" | "n"  | "l"  | "p" }
    black_piece = { "K" | "R"  | "B" | "G" | "S" | "N"  | "L"  | "P" }
    white_hand_piece = { "r"  | "b" | "g" | "s" | "n"  | "l"  | "p" }
    black_hand_piece = { "R"  | "B" | "G" | "S" | "N"  | "L"  | "P" }
    npieces = { "18" | "17" | "16" | "15" | "14" | "13" | "12" | "11" | "10"
              | "9" | "8" | "7" | "6" | "5" | "4" | "3" | "2" | "1" }
    
    moves = { one_move ~ (WS ~ one_move)* }

    one_move = { drop | board_move }
    drop = { black_piece ~ "*" ~ square }
    board_move = { square ~ square ~ "+"? }

    square = { file ~ rank }
    file = { '1'..'9' }
    rank = { 'a'..'i' }

go = ${ "go" ~ (WS ~ go_sub_cmd)*  }

    go_sub_cmd = _{
        searchmoves |          
        ponder |
        movetime |
        byoyomi |
        movestogo |
        wtime |
        btime |
        winc |
        binc |
        depth |
        nodes | 
        mate |
        infinite
    }

    searchmoves = { "searchmoves" ~ WS ~ moves}
    ponder = { "ponder" }
    movetime = { "movetime" ~ WS ~ millisecs }
    byoyomi = { "byoyomi" ~ WS ~ millisecs }
    wtime = { "wtime" ~ WS ~ millisecs }
    btime = { "btime" ~ WS ~ millisecs }
    winc = { "winc" ~ WS ~ millisecs }
    binc = { "binc" ~ WS ~ millisecs }
    movestogo = { "movestogo" ~ WS ~ digits }
    depth = { "depth" ~ WS ~ digits }
    nodes = { "nodes" ~ WS ~ digits }
    mate = { "mate" ~ WS ~ (infinite | millisecs) }
    infinite = { "infinite" }

//
// EngineMessage: Engine to GUI
//

id = ${ "id" ~ WS ~ (id_name | id_author) }

    id_name = ${ "name" ~ WS ~ tokens }
    id_author = ${ "author" ~ WS ~ tokens }

usiok = { "usiok"  }

readyok = { "readyok"  }

bestmove = ${ "bestmove" ~ WS ~ ((one_move ~ (WS ~ "ponder" ~ WS ~ ponder_move)?) | resign | win)  }

    ponder_move = { one_move }
    resign = { "resign" }

copyprotection = ${ "copyprotection" ~ WS ~ status_check  }

registration = ${ "registration" ~ WS ~ status_check  }

    status_check = { "checking" | "ok" | "error" }

option = ${ "option" ~ WS ~ (check_option | spin_option | combo_option | string_option | button_option | filename_option)  }

    check_option = ${ option_name ~ WS ~ "type" ~ WS ~ "check" ~ (WS ~ "default" ~ WS ~ check_default)? }
    spin_option = ${ option_name ~ WS ~ "type" ~ WS ~ "spin" ~ (WS ~ "default" ~ WS ~ spin_default)? ~ (WS ~ spin_min)? ~ (WS ~ spin_max)? }
    combo_option = ${ option_name ~ WS ~ "type" ~ WS ~ "combo" ~ (WS ~ "default" ~ WS ~ combo_default?) ~ (WS ~ "var" ~ WS ~ var_token)* }
    string_option = ${ option_name ~ WS ~ "type" ~ WS ~ "string" ~ (WS ~ "default" ~ WS ~ token)? }
    button_option = ${ option_name ~ WS ~ "type" ~ WS ~ "button" }
    filename_option = ${ option_name ~ WS ~ "type" ~ WS ~ "filename" ~ (WS ~ "default" ~ WS ~ token)? }

    option_name = ${ "name" ~ WS ~ name_token }
    name_token = ${ !("type") ~ token }
    
    check_default = { ^"true" | ^"false" }
    spin_default = { integer }    
    spin_min = ${ "min" ~ WS ~ integer }
    spin_max = ${ "max" ~ WS ~ integer }
    combo_default = ${ var_token }
    var_token = ${ !("default" | "var") ~ token}
    
info = ${ "info" ~ (WS ~ info_attr)+  }

    info_attr = _{
        info_depth |
        info_seldepth |
        info_time | 
        info_nodes | 
        info_currmovenumber | 
        info_currmove | 
        info_hashfull | 
        info_nps |
        info_cpuload | 
        info_pv | 
        info_multipv | 
        info_refutation | 
        info_currline |
        info_score_cp |
        info_score_mate |
        info_string
    }

    info_depth = ${ "depth" ~ WS ~ digits }
    info_seldepth = ${ "seldepth" ~ WS ~ digits }
    info_time = ${ "time" ~ WS ~ millisecs }
    info_nodes = ${ "nodes" ~ WS ~ digits }
    info_currmove = ${ "currmove" ~ WS ~ one_move }
    info_currmovenumber = ${ "currmovenumber" ~ WS ~ digits }
    info_hashfull = ${ "hashfull" ~ WS ~ digits }
    info_nps = ${ "nps" ~ WS ~ digits }
    info_cpuload = ${ "cpuload" ~ WS ~ digits }    
    info_pv = ${ "pv" ~ WS ~ moves }
    // multiple multipv commands need to be sent in separate lines
    // so multipv should perhaps be more restrictive
    info_multipv = ${ "multipv" ~ WS ~ digits }
    info_refutation = ${ "refutation" ~ WS ~ moves }
    info_currline = ${ "currline" ~ WS ~ (cpunr ~ WS)? ~ moves }

        cpunr = ${ digits }

    info_score_cp = ${ "score" ~ WS ~ "cp" ~ WS ~ integer ~ (WS ~ (lowerbound | upperbound))? }
    info_score_mate = ${ "score" ~ WS ~ "mate" ~ WS ~ ((integer ~ (WS ~ (lowerbound | upperbound))?) | (plus|minus)) }

        lowerbound = { "lowerbound" }
        upperbound = { "upperbound" }

    info_string = ${ "string" ~ WS ~ tokens }

//
// helpers 
// 

// a token is a contiguous sequence of printable 7-bit ASCII characters (excluding whitespace)
token = @{ ('!'..'~')+ }

// tokens is a sequence of one or more white-space-separated tokens (excluding newlines)
tokens = @{ token ~ (WS ~ token)* }

// digits is a contiguous sequence of ascii digits
digits = @{ ASCII_DIGIT+ }

millisecs = { digits }
integer = { (plus | minus)? ~ digits }
plus = { "+" }
minus = { "-" }




